// Copyright 2025 The Outline Authors
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

// Outline template for android/build.gradle.
// This mirrors the Capacitor-generated file with our customizations (Gradle/AGP versions,
// Java 17 enforcement, local Maven repo with Outline artifacts).
// Original source: <project>/android/build.gradle produced by Capacitor CLI.
// Why: Capacitor reverts our AGP/JDK configuration and strips the local Maven repository needed
// for Outline's tun2socks artifacts, so we restore them via this template.
// Update this template if Capacitor's default file changes.

// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.7.3'
        classpath 'com.google.gms:google-services:4.4.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.9.23"
    }
}

apply from: "variables.gradle"

allprojects {
    repositories {
        maven { url uri("$rootDir/../../../output/client/android") }
        google()
        mavenCentral()
    }

    tasks.withType(JavaCompile).configureEach {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    afterEvaluate { p ->
        if (p.extensions.findByName("android") != null) {
            p.extensions.getByName("android").compileOptions {
                sourceCompatibility = JavaVersion.VERSION_17
                targetCompatibility = JavaVersion.VERSION_17
            }
            def kotlinExt = p.extensions.findByName("kotlinOptions")
            if (kotlinExt != null) {
                kotlinExt.jvmTarget = "17"
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

